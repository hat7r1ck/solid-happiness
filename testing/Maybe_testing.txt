
| fields _time logName jsonPayload.sanitizationResult.sanitizationVerdictReason jsonPayload.sanitizationInput.text
| rename jsonPayload.sanitizationInput.text as InputText
| eval project=mvindex(split(logName,"/"),1)
| eval projectid=mvindex(split(project,"-"),2)
| regex field=InputText "(?x)(\b(?!(000|666|9\d\d))\d{3}-?\d{2}-?\d{4}\b|(?:\d[ -]?){13,19}\b|\b9\d{2}[ -]?(?:7[0-9]|8[0-8]|9[0-2])[4-9]\d[ -]?\d{4}\b|\b\d{9}\b|\bAIza[0-9A-Za-z\-_]{35}\b|\b[a-z0-9\-_]+@[a-z0-9\-]+\.iam\.gserviceaccount\.com\b|\bya29\.[0-9A-Za-z\-_]+\b|\b1\/\/[0-9A-Za-z\-_]+\b|\"private_key_id\"\s*:\s*\"[a-f0-9]{40}\")"
| rex max_match=0 field=InputText "(?x)
(?<ssn>\b(?!(000|666|9\d\d))\d{3}-?\d{2}-?\d{4}\b)
|(?<ccn>\b(?:\d[ -]?){13,19}\b)
|(?<itin>\b9\d{2}[ -]?(?:7[0-9]|8[0-8]|9[0-2])[4-9]\d[ -]?\d{4}\b)
|(?<fan>\b\d{9}\b)
|(?<gcp_api_key>\bAIza[0-9A-Za-z\-_]{35}\b)
|(?<gcp_email>\b[a-z0-9\-_]+@[a-z0-9\-]+\.iam\.gserviceaccount\.com\b)
|(?<access_token>\bya29\.[0-9A-Za-z\-_]+\b)
|(?<refresh_token>\b1\/\/[0-9A-Za-z\-_]+\b)
|\"private_key_id\"\s*:\s*\"(?<private_key_id>[a-f0-9]{40})\"
"
| eval matches=mvappend(
    if(isnotnull(ssn),"SSN",null()),
    if(isnotnull(itin),"ITIN",null()),
    if(isnotnull(ccn),"CCN",null()),
    if(isnotnull(fan),"FAN",null()),
    if(isnotnull(gcp_api_key),"GCP_API_KEY",null()),
    if(isnotnull(gcp_email),"GCP_EMAIL",null()),
    if(isnotnull(access_token),"ACCESS_TOKEN",null()),
    if(isnotnull(refresh_token),"REFRESH_TOKEN",null()),
    if(isnotnull(private_key_id),"PRIVATE_ID_KEY",null())
  )
| eval matches=mvfilter(isnotnull(matches))
| where mvcount(matches)>0
| sort 0 - _time
| eval input_hash=md5(InputText)
| stats
    count as event_count
    earliest(_time) as first_seen
    latest(_time) as last_seen
    first(jsonPayload.sanitizationResult.sanitizationVerdictReason) as verdict_reason
    first(matches) as match_types
    first(ssn) as ssn
    first(itin) as itin
    first(ccn) as ccn
    first(fan) as fan
    first(gcp_api_key) as gcp_api_key
    first(gcp_email) as gcp_email
    first(access_token) as access_token
    first(refresh_token) as refresh_token
    first(private_key_id) as private_key_id
    first(InputText) as sample_InputText
  by project projectid input_hash
| fields - input_hash

	•	Pushed SDP verdict reason and AITACG scoping into the base search, drops noise before any heavy parsing.
	•	Added fields early, reduces payload size carried through regex and rex.
	•	Renamed jsonPayload.sanitizationInput.text to InputText, avoids repeating a long field path and keeps later commands cheaper to read and maintain.
	•	Added a fast prefilter regex before rex, avoids running extraction on events that cannot match any target patterns.
	•	Replaced rex parsing of logName and project with split() operations, less CPU than regex for delimiter-based strings.
	•	Converted output from event-level table to a deduped stats view keyed on md5(InputText), cuts row count and improves runtime while still keeping a representative sample prompt plus timestamps and counts.
